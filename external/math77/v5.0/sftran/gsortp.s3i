      subroutine GSORTP (COMPAR, N, P)
noindent
C     .  Copyright (C) 1989, California Institute of Technology.
C     .  All rights reserved.  U. S. Government sponsorship under
C     .  NASA contract NAS7-918 is acknowledged.
C>> 1991-04-02  GSORTP  Snyder  Repair no permutation vector if m-n < 10
C>> 1988-11-22  GSORTP  Snyder  Initial code.
c
c     Sort an N-vector of objects of unknown type and organization.
c     P is set so that the P(J)'th element of the original sequence is
c     the J'th element of the sorted sequence.  The order is defined by
c     the integer function COMPAR.  An invocation COMPAR(I,J) should
c     return -1 if the I'th element of the data is to preceed the J'th
c     element in the sorted sequence, +1 if the J'th element is to
c     preceed the I'th element in the sorted sequence, and 0 if the I'th
c     and J'th elements are the same.
c
c     This subprogram is unaware of the data, and cannot manipulate it.
c     It is the caller's responsibility to make the data known to the
c     COMPAR function.
c
      integer COMPAR, N, P(*)
      external COMPAR
c
c     *****     Local Variables     ************************************
c
c BL      is the left bound of the sub-array to be sorted at the next
c         step.
c BR      is the right bound of the sub array to be sorted at the next
c         step.
c CL      is the current left bound of the unsorted sub-array.
c CR      is the current right bound of the unsorted sub-array.
c PARTN   is the partition element.
c PTEMP   holds elements of P during exchanges.
c STACKL  keeps track of the left bounds of sub-arrays waiting to be
c         sorted.
c STACKR  keeps track of the right bounds of sub-arrays waiting to be
c         sorted.
c STKTOP  keeps track of the top of the stacks.
c
      integer BL,BR,CL,CR,PARTN,PTEMP
      integer STACKL(32),STACKR(32),STKTOP
indent
c
c     *****     Executable Statements     ******************************
c
      do for cl = 1, n
         p(cl)=cl
      end for
      if (n.ge.10) then
         stktop=1
         stackl(1)=1
         stackr(1)=n
         do until (stktop.eq.0)
            bl=stackl(stktop)
            br=stackr(stktop)
            stktop=stktop-1
c           Choose a partitioning element.  Use the median of the first,
c           middle and last elements.  Sort them so the extreme elements
c           can serve as sentinels during partitioning.
            cl=(bl+br)/2
            partn=p(cl)
            if (compar(p(bl),partn).gt.0) then
               p(cl)=p(bl)
               p(bl)=partn
               partn=p(cl)
            end if
            if (compar(p(bl),p(br)).gt.0) then
               ptemp=p(bl)
               p(bl)=p(br)
               p(br)=ptemp
            end if
            if (compar(partn,p(br)).gt.0) then
               p(cl)=p(br)
               p(br)=partn
               partn=p(cl)
            end if
            p(cl)=p(br-1)
            p(br-1)=partn
c           Partition the sub-array around PARTN.  Exclude the above
c           considered elements from partitioning because they're al-
c           ready in the correct subfiles.  Stop scanning on equality to
c           prevent files containing equal values from causing a loop.
            cl=bl
            cr=br-1
            do forever
               do until (compar(p(cl),partn).ge.0)
                  cl=cl+1
               end until
               do until (compar(p(cr),partn).le.0)
                  cr=cr-1
               end until
               if (cl.gt.cr) exit forever
               ptemp=p(cl)
               p(cl)=p(cr)
               p(cr)=ptemp
            end forever
c           Put sub-arrays on the stack if they're big enough.  Put the
c           larger under the smaller, so the smaller will be done next.
c           This makes the upper bound of the stack depth log2 (n).
c           (The "Hibbard" modification of quicksort).
            if (cl-bl .gt. br-cr) then
               if (cl-bl.gt.10) then
                  stktop=stktop+1
                  stackl(stktop)=bl
                  stackr(stktop)=cr
               end if
               if (br-cr.gt.10) then
                  stktop=stktop+1
                  stackl(stktop)=cl
                  stackr(stktop)=br
               end if
            else
               if (br-cr.gt.10) then
                  stktop=stktop+1
                  stackl(stktop)=cl
                  stackr(stktop)=br
               end if
               if (cl-bl.gt.10) then
                  stktop=stktop+1
                  stackl(stktop)=bl
                  stackr(stktop)=cr
               end if
            end if
         end until
      end if
c     Clean up small subfiles by using insertion sort on everything.
      do for cr = 2, n
         ptemp=p(cr)
         cl=cr
         do while (compar(p(cl-1),ptemp).gt.0)
            p(cl)=p(cl-1)
            cl=cl-1
            if (cl.le.1) exit while
         end while
         p(cl)=ptemp
      end for
c
      return
c
      end program
