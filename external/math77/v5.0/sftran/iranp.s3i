      integer function IRANP(XMEAN)
inline(!)
noindent
C     .  Copyright (C) 1989, California Institute of Technology.
C     .  All rights reserved.  U. S. Government sponsorship under
C     .  NASA contract NAS7-918 is acknowledged.
c>> 1992-03-16 CLL
c>> 1991-11-26 CLL Reorganized common. Using RANCM[A/D/S].
c>> 1991-11-22 CLL Added call to RAN0, and SGFLAG in common.
c>> 1991-01-15 CLL Reordered common contents for efficiency.
C>> 1990-01-23 CLL  Making names in common same in all subprogams.
C>> 1987-04-23 IRANP  Lawson  Initial code.
c        Returns one pseudorandom integer from the Poisson distribution
c     with mean and variance = XMEAN.
c        The probability of occurrence of the nonnegative integer k in
c     the Poisson distribution with mean XMEAN is
 
c                P(k) = exp(-XMEAN) * XMEAN**k / k!
 
c        Let SUM(n) denote the sum for k = 0 to n of P(k).
c     Let U be a random sample from a uniform
c     distribution on [0.0, 1.0].  The returned value of IRANP will be
c     the smallest integer such that S(IRANP) .ge. U.
c     This variable has mean and variance = XMEAN.
c     Reference: Richard H. Snow, Algorithm 342, Comm ACM, V. 11,
c     Dec 1968, p. 819.
c     Code based on subprogram written for JPL by Stephen L. Ritchie,
c     Heliodyne Corp. and Wiley R. Bunton, JPL, 1969.
c     Adapted to Fortran 77 for the JPL MATH77 library by C. L. Lawson &
c     S. Y. Chiu, JPL, Apr 1987.
c     ------------------------------------------------------------------
c                    Subprogram argument and result
c
c     XMEAN [in]  Mean value for the desired distribution.
c           Must be positive and not so large that exp(-XMEAN)
c           will underflow.  For example, on a machine with underflow
c           limit 0.14E-38, XMEAN must not exceed 88.
c
c     IRANP [Returned function value]  Will be set to a nonnegative
c           integer value if computation is successful.
c           Will be set to -1 if XMEAN is out of range.
c     ------------------------------------------------------------------
c     Intrinsic functions referenced: EXP, LOG, MIN, NINT, REAL
c     MATH77 subprograms referenced: R1MACH, SERM1, SERV1, SRANUA, RAN0
c     Other MATH77 subprograms needed: ERMSG, ERFIN, I1MACH
c     Common referenced: /RANCMS/, /RANCMA/
c     ------------------------------------------------------------------
      integer M, NMAX
      parameter(M = 97, NMAX = 84)
      real    SNUMS(M), ELIMIT, MONE, R1MACH, S, SPREV, SUM(0:NMAX)
      real    TERM, TLAST, TWO
      real    U, XMEAN, XMSAVE, ZERO
      integer LAST, N, NMID
      logical FIRST
      parameter(ZERO = 0.0E0, MONE = -1.0E0, TWO = 2.0E0)
      common/RANCMS/SNUMS
c
      integer DPTR, SPTR
      logical DGFLAG, SGFLAG
      common/RANCMA/DPTR, SPTR, DGFLAG, SGFLAG
c
      save   /RANCMA/, /RANCMS/
      save    ELIMIT, FIRST, LAST, NMID, SUM, TLAST, XMSAVE
      data    FIRST / .true. /
      data    ELIMIT / ZERO /
c     ------------------------------------------------------------------
indent
      if(FIRST) then
         do( SET FOR NEW XMEAN )
         FIRST = .false.
         call RAN0
      elseif(XMEAN .ne. XMSAVE) then
         do( SET FOR NEW XMEAN )
      endif
c
      do( SET U = UNIFORM RANDOM NUMBER )
c
      if(U .gt. SUM(LAST)) then
         do( COMPUTE MORE SUMS )
      else
         do( SEARCH THROUGH STORED SUMS )
      endif
      return
c     ------------------------------------------------------------------
      procedure( SET FOR NEW XMEAN )
c
c     R1MACH(1) gives the underflow limit.
c     ELIMIT gives a limit for arguments to the exponential function
c     such that if XMEAN .le. ELIMIT,  exp(-XMEAN) should not
c     underflow.
c
      if(ELIMIT .eq. ZERO) ELIMIT = -log(R1MACH(1) * TWO)
      if(XMEAN .le. ZERO  .or.  XMEAN .gt. ELIMIT) then
         call SERM1('IRANP',1, 0,'Require 0 .lt. XMEAN .le. ELIMIT',
     *      'XMEAN',XMEAN,',')
         call SERV1('ELIMIT',ELIMIT,'.')
         IRANP = -1
         return
      endif
      LAST = 0
      TLAST = exp(-XMEAN)
      SUM(0) = TLAST
      XMSAVE = XMEAN
      NMID = nint(XMEAN)
      end proc ! ( SET FOR NEW XMEAN )
c     ------------------------------------------------------------------
      procedure ( SET U = UNIFORM RANDOM NUMBER )
c
      SPTR = SPTR - 1
      if(SPTR .eq. 0) then
         call SRANUA(SNUMS,M)
         SPTR = M
      endif
      U = SNUMS(SPTR)
      end proc !( SET U = UNIFORM RANDOM NUMBER )
c     ------------------------------------------------------------------
      procedure( COMPUTE MORE SUMS )
c
         N = LAST
         SPREV = SUM(LAST)
         TERM = TLAST
         do forever
            N = N + 1
            TERM = TERM * XMEAN / real(N)
            S = SPREV + TERM
            if(S .eq. SPREV) then
               IRANP = N
               exit forever
            endif !(S...)
            if(N .le. NMAX) then
               LAST = N
               SUM(LAST) = S
               TLAST = TERM
            endif
            if(U .le. S) then
               IRANP = N
               exit forever
            endif
            SPREV = S
         end forever
      end proc !( COMPUTE MORE SUMS )
c     ------------------------------------------------------------------
      procedure( SEARCH THROUGH STORED SUMS )
c
c     Here we already know that U .le. SUM(LAST).
c     It is most likely that U will be near SUM(NMID).
c
         if(NMID .lt. LAST) then
            if( U .gt. SUM(NMID) ) then
               do for N = NMID+1, LAST
                  if(U .le. SUM(N)) then
                     IRANP = N
                     exit proc
                  endif !(U...)
               end for ! N
            endif !(U...)
         endif !(NMID...)
c
         do for N = min(NMID, LAST)-1, 0,-1
            if(U .gt. SUM(N)) then
               IRANP = N+1
               exit proc
            endif !(U...)
         end for ! N
         IRANP = 0
      end proc !( SEARCH THROUGH STORED SUMS )
      end program ! IRANP
